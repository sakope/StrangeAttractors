#pragma kernel Emit
#pragma kernel Iterator

#define THREAD_X 128
#define THREAD_Y 1
#define THREAD_Z 1
#define DT 0.033

struct Params
{
    float3 emitPos;
    float3 position;
    float3 velocity; //xyz = velocity
    float  life;
    float2 size;     // x = current size, y = target size.
    float4 color;
};
            
RWStructuredBuffer<Params> buf;

// Aizawa Attractor parameters
float a;
float b;
float c;
float d;
float e;
float f;

float3 AizawaAttractor(float3 pos)
{
    float dxdt = (pos.z - b) * pos.x - d * pos.y;
    float dydt = d * pos.x + (pos.z - b) * pos.y;
    float dzdt = c + a * pos.z - (pow(pos.z, 3.0) / 3.0) - (pow(pos.x, 2.0) + pow(pos.y, 2.0)) * (1 + e * pos.z) + f * pos.z * (pow(pos.x, 3.0));
    return float3(dxdt, dydt, dzdt) * DT;
}

[numthreads(THREAD_X, THREAD_Y, THREAD_Z)]
void Emit(uint id : SV_DispatchThreadID)
{
    Params p = buf[id];
    p.life = (float)id*-1e-05;
    p.position = p.emitPos;
    p.size.x = p.size.y;
    buf[id] = p;
}

[numthreads(THREAD_X, THREAD_Y, THREAD_Z)]
void Iterator(uint id : SV_DispatchThreadID)
{
    Params p = buf[id];
    p.life.x += DT;
    if (p.life.x > 0)
    {
        p.velocity = AizawaAttractor(p.position);
        p.position += p.velocity;
    }
    buf[id] = p;
}
